prototype(CodeQ.Site:Components.Navigation.Item) < prototype(Neos.Fusion:Tag) {
	tagName = 'a'
	attributes {
		href = NodeUri {
			node = ${node}
		}
		class = Neos.Fusion:RawArray {
			base = 'Navigation-item'
			level = ${'Navigation-itemLevel' + (node.depth - 2)}
			current = 'isCurrent'
			current.@if.isCurrent = ${node == documentNode}
			active = 'isActive'
			active.@if.isActive = ${(documentNode == site && node == documentNode) || node == documentNode || (node.depth != 2 && (q(documentNode).parents('[_identifier = "' + node.identifier + '"]').count() > 0))}
		}
	}
	content = ${node.properties.title}
}

prototype(CodeQ.Site:Components.Navigation.LevelRenderer) < prototype(Neos.Fusion:Array) {
	title = CodeQ.Site:Components.Navigation.Item
	nextLevel = Neos.Fusion:Collection {
		@if.notEmpty = ${this.collection.count() > 0}
		collection = ${q(node).children('[instanceof Neos.Neos:Document][_hiddenInIndex != TRUE][hideFromMainMenu != TRUE]')}
		itemName = 'node'
		itemRenderer = CodeQ.Site:Components.Navigation.LevelRenderer
	}
}

prototype(CodeQ.Site:Components.Navigation) < prototype(Neos.Fusion:Value) {
	@context.node = ${site}
	value = CodeQ.Site:Components.Navigation.LevelRenderer
	@cache {
		mode = 'cached'
		entryIdentifier.node = ${documentNode}
		entryTags {
			1 = 'NodeType_Neos.Neos:Document'
		}
	}
}
